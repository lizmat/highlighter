use Test;
use highlighter;

plan 54;

my $haystack = "foo bar baz bar zop";

my sub test-co($needle, $capture, \result, $description) is test-assertion {
    is-deeply columns($haystack, $needle, |$capture),
      result,
      $description;

    is-deeply columns($haystack, ($needle, "frobnicate"), |$capture),
      result,
      "$description (list first)";

    is-deeply columns($haystack, ("frobnicate", $needle), |$capture),
      result,
      "$description (list last)";
}

test-co "bar", \(:type<words>),
  (5,13),
  'did "bar" get highlighted as a word';

test-co "BAR", \(:type<words>, :ignorecase),
  (5,13),
  'did "BAR" get highlighted as a word case-insensitively';

test-co "ar", \(:type<words>),
  (),
  'did "ar" NOT get highlighted as a word';

test-co "ba", \(:type<contains>),
  (5,9,13),
  'did "ba" get highlighted as a string';

test-co "BA", \(:type<contains>, :ignorecase),
  (5,9,13),
  'did "BA" get highlighted as a string case-insensitively';

test-co "zz", \(:type<words>),
  (),
  'did "zz" NOT get highlighted as a string';

test-co "foo", \(:type<starts-with>),
  (1,),
  'did "foo" get highlighted at beginning';

test-co "FOO", \(:type<starts-with>, :ignorecase),
  (1,),
  'did "FOO" get highlighted at beginning case-insensitively';

test-co "zz", \(:type<starts-with>),
  (),
  'did "zz" NOT get highlighted at beginning';

test-co "zop", \(:type<ends-with>),
  (17,),
  'did "zop" get highlighted at end';

test-co "ZOP", \(:type<ends-with>, :ignorecase),
  (17,),
  'did "ZOP" get highlighted at end case-insensitively';

test-co "zz", \(:type<ends-with>),
  (),
  'did "zz" NOT get highlighted at end';

test-co / << b.r >> /, \(),
  (5,13),
  'did "b.r" get highlighted as a match';

test-co rx:i/ B.R /, \(),
  (5,13),
  'did "B.R" get highlighted as a match case-insensitively';

test-co / zz /, \(),
  (),
  'did "zz" NOT get highlighted as a match';

test-co 'ar', \(),
  (6,14),
  'did "ar" get highlighted';

test-co *.contains("foo"), \(),
  (),
  'did we get no highlighting at all';

test-co / <( ba \w )> /, \(),
  (5,9,13),
  'did we get correct highlighting with capture markers';

# vim: expandtab shiftwidth=4
